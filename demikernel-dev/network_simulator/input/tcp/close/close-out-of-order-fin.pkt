// Tests for remote close with an out of order FIN.

// Establish a connection.
+.0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 500
+.2 connect(500, ..., ...) = 0

// Send SYN segment.
+.0 TCP > S seq 65535(0) win 65535 <mss 1450, wscale 0>
// Receive SYN-ACK segment.
+.1 TCP < S. seq 50(0) ack 65536 win 65535 <mss 1450, wscale 0>

// Succeed to establish connection.
+.0 wait(500, ...) = 0

// Send ACK on SYN-ACK segment.
+.0 TCP > . seq 65536(0) ack 51 win 65535 <nop>

// Send data.
+.1 write(500, ..., 1000) = 1000

// Send data packet.
+0 TCP > P. seq 65536(1000) ack 51 win 65535 <nop>

// Receive out of order FIN segment.
+.1 TCP < F. seq 1051(0) ack 66536 win 65535 <nop>

// Send finished because out of order FIN acked the data.
+.0 wait(500, ...) = 0

// Send ACK packet for out of order data.
+.0 TCP > . seq 66536(0) ack 51 win 65535 <nop>

// Receive data packet
+.1 TCP < P. seq 51(1000) ack 66536 win 65535 <nop>

// Send ACK packet for data and FIN.
+.0 TCP > . seq 66536(0) ack 1052 win 64534 <nop>

// Close connection.
+.2 close(500) = 0

// Send FIN segment.
+.0 TCP > F. seq 66536(0) ack 1052 win 64534 <nop>

// Receive ACK on FIN segment.
+.1 TCP < . seq 1052(0) ack 66537 win 64534 <nop>

// Succeed to close connection immediately.
+.0 wait(500, ...) = 0
